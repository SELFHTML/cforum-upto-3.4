dnl
dnl \file configure.in
dnl \author Christian Kruse, <ckruse@wwwtech.de>
dnl
dnl configure input script for the classic forum project
dnl

dnl {{{ Initial comments
dnl $Header: /home/users/cvs/selfforum/configure.in,v 1.10 2003/11/24 12:53:27 ckruse Exp $
dnl $Log: configure.in,v $
dnl Revision 1.10  2003/11/24 12:53:27  ckruse
dnl bugfix with charset handling
dnl
dnl Revision 1.9  2003/10/20 08:57:39  ckruse
dnl Heavy developement
dnl
dnl
dnl }}}

sinclude(m4/ax_compare_version.m4)
sinclude(m4/ax_path_bdb.m4)
sinclude(m4/cf_check_type.m4)

AC_INIT

AM_CONFIG_HEADER(src/config.h)
AM_INIT_AUTOMAKE(cforum,2.0)

AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL

CFLAGS="$CFLAGS -D_REENTRANT -Wall -W -pipe -D_GNU_SOURCE"
LDFLAGS="$LDFLAGS -L/usr/local/lib"

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h pthread.h sys/ipc.h sys/shm.h sys/sem.h)

AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday mkdir mktime socket strerror strstr strtol strtoul strtoll strtoull snprintf sleep getenv)

AC_CHECK_FUNC(strdup,[],[AC_DEFINE(NOSTRDUP,1,[Defined if no strdup() is available])])
AC_CHECK_FUNC(strndup,[],[AC_DEFINE(NOSTRNDUP,1,[Defined if no strndup() is available])])
AC_CHECK_FUNC(getline,[],[AC_DEFINE(HAS_NO_GETLINE,1,[Defined if not getline() is available])])
AC_CHECK_FUNC(getdelim,[],[AC_DEFINE(HAS_NO_GETDELIM,1,[Defined if no getdelim is available])])

dnl
dnl check for libs and compiler specifics...
dnl

dnl check for iconv funktion
AC_CHECK_FUNC(iconv,ICONV="",[
  AC_CHECK_LIB(iconv,iconv,ICONV="-liconv",[AC_MSG_ERROR([iconv library cannot be found])])
])

dnl check for dlopen funktion
AC_CHECK_FUNC(dlopen,DL="",[
  AC_CHECK_LIB(dl,dlopen,DL="-ldl",[AC_MSG_ERROR([dl library cannot be found])])
])

dnl lets check for gdome-config and the right version of gdome
AC_PATH_PROG(GDOMECFG, gdome-config)
if test ! -n "$GDOMECFG" ; then
  AC_MSG_ERROR([cannot find gdome-config. Be sure that GDOME2 is installed and gdome-config is in your PATH])
fi

AC_PATH_PROG(LIBWWWCFG, libwww-config)
if test ! -n "$LIBWWWCFG" ; then
  AC_MSG_ERROR([cannot find libwww-config. Be sure that libwww is installed and libwww-config is in your PATH])
fi

AC_PATH_PROG(PCRECFG, pcre-config)
if test ! -n "$PCRECFG" ; then
  AC_MSG_ERROR([cannot find pcre-config. Be sure that libpcre ist installed and pcre-config is in your PATH])
fi

GDOME_CFLAGS=`$GDOMECFG --cflags`
GDOME_LIBS=`$GDOMECFG --libs`

LIBWWW_CFLAGS=`$LIBWWWCFG --cflags`
LIBWWW_LIBS=`$LIBWWWCFG --libs`

PCRE_CFLAGS=`$PCRECFG --cflags`
PCRE_LIBS=`$PCRECFG --libs`

CFLAGS="$LIBWWW_CFLAGS $GDOME_CFLAGS $CFLAGS"

AC_SUBST(LIBWWW_LIBS)
AC_SUBST(GDOME_LIBS)
AC_SUBST(PCRE_LIBS)
AC_SUBST(ICONV)
AC_SUBST(DL)


dnl lets check for -pthread
AC_MSG_CHECKING(for -pthread arg to compiler)
CFLAGS_SAVE="$CFLAGS"
CFLAGS="$CFLAGS -pthread"
AC_TRY_LINK([ #include <pthread.h> ],
  [ pthread_create(0,0,0,0); ],
  [
    AC_MSG_RESULT(yes)
    PTHREAD_LIB="-pthread"
  ],
  [
    AC_MSG_RESULT(no)
    CFLAGS="$CFLAGS_SAVE"
    AC_CHECK_LIB(pthread, pthread_create,[ PTHREAD_LIB="-lpthread" ],AC_MSG_ERROR([pthread library cannot be found]))
  ]
)

CFLAGS="$CFLAGS_SAVE"
AC_SUBST(PTHREAD_LIB)

AC_TRY_LINK([ #include <pthread.h> ],
  [ pthread_getprio(NULL); ],
  [ AC_DEFINE(_PTHREAD_GET_SET_PRIO,1,[Defined if pthread_get_prio() und pthread_set_prio() is available]) ],
  [ ]
)

AC_ARG_ENABLE(debug,
  [AC_HELP_STRING(
    [--enable-debug],
    [enable debugging mode])],
  [ AC_DEFINE(DEBUG,1,[Defined in debugging modus]) ],
  [ fake=1 ]
)

AC_ARG_ENABLE(sorting,
  [AC_HELP_STRING(
    [--enable-sorting],
    [enable sorting APIs (beware of the performance consequences!)])],
  [ fake=1 ],
  [ AC_DEFINE(CF_NO_SORTING,1,[Defined if sorting is not enabled]) ]
)

dnl
dnl shared memory is something like an after burner :)
dnl
AC_ARG_ENABLE(shared-mem,
  [AC_HELP_STRING(
    [--enable-shared-mem],
    [enable shared memory (*much* more performance)])],
  [ AC_DEFINE(CF_SHARED_MEM,1,[Defined if shared memory should be used]) ],
  [ fake=1 ]
)

AC_ARG_WITH(message-dir,
  [AC_HELP_STRING(
    [--with-message-dir=DIR],
    [the directory where the message files are located])],
  [fake=1],
  [ with_message_dir="${localstatedir}/cforum/messages" ]
)

AC_ARG_WITH(data-dir,
  [AC_HELP_STRING(
	  [--with-data-dir=DIR],
		[the directory where data files are located])],
	[ DATADIR="${with_data_dir}" ],
	[ DATADIR="${localstatedir}/cforum/data" ]
)

AC_ARG_WITH(archive-dir,
  [AC_HELP_STRING(
    [--with-archive-dir=DIR],
    [the directory where the archived files are located])],
  [fake=1],
  [ with_archive_dir="${localstatedir}/cforum/archive" ]
)

AC_ARG_WITH(config-dir,
  [AC_HELP_STRING(
	  [--with-config-dir=DIR],
		[the directory where the configuration files are located])],
	[ CONFIGDIR="${with_config_dir}" ],
	[ CONFIGDIR="${sysconfdir}/cforum/config" ]
)

AC_ARG_WITH(perl-mods,
  [AC_HELP_STRING(
	  [--with-perl-mods=DIR],
		[the directory where the configuration files are located])],
	[ PERLMODULES="${with_perl_mods}" ],
	[ PERLMODULES="${sysconfdir}/cforum/" ]
)

AC_ARG_WITH(socket-path,
  [AC_HELP_STRING(
     [--with-socket-path=DIR],
     [the path where the socket should be located])],
  [fake=1],
  [ with_socket_path="/tmp/selfforum_socket" ]
)
AC_ARG_WITH(log-dir,
  [AC_HELP_STRING(
    [--with-log-dir=DIR],
    [the path where the forum log files should be located])],
  [fake=1],
  [ with_log_dir="${localstatedir}/cforum/logs" ]
)
AC_ARG_WITH(proto-file,
  [AC_HELP_STRING(
    [--with-proto-file=PATH],
    [the path where the protocol file for bad signals is located (used for debugging)])],
  [fake=1],
  [ with_proto_file="${localstatedir}/cforum/sigprotocol.txt" ]
)
AC_DEFINE_UNQUOTED(PROTOCOL_FILE,"${with_proto_file}",[needed to collect debugging infos])

AX_PATH_BDB([3],[
    BDB_LIB="$BDB_LIBS";
    CFLAGS="$CFLAGS $BDB_CPPFLAGS"
  ],
  [AC_MSG_ERROR([no BerkeleyDB could be found])]
)

AC_CHECK_LIB(
  idn,
  idna_to_ascii_8z,
  IDN="-lidn",
  AC_MSG_ERROR([idn library cannot be found])
)

AC_SUBST(DOCROOT)
AC_SUBST(DATADIR)
AC_SUBST(PERLMODULES)
AC_SUBST(CONFIGDIR)
AC_SUBST(IDN)
AC_SUBST(BDB_LIB)
AC_SUBST(with_message_dir)
AC_SUBST(with_archive_dir)
AC_SUBST(with_socket_path)
AC_SUBST(with_log_dir)

dnl
dnl check for programs
dnl
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(LN,ln)
AC_PATH_PROG(LS,ls)
AC_PATH_PROG(GREP,grep)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(INSTALL,install)
AC_PATH_PROG(FLEX,flex)
AC_PATH_PROG(SED,sed)
AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(MAKEPGM,make)

dnl
dnl check for compiler characteristics
dnl

AC_CHECK_SIZEOF(short,"")
AC_CHECK_SIZEOF(int,"")
AC_CHECK_SIZEOF(long,"")
AC_CHECK_SIZEOF(long long,"")

CF_CHECK_TYPE(u_char,1)
CF_CHECK_TYPE(int16_t,1)
CF_CHECK_TYPE(u_int16_t,1)
CF_CHECK_TYPE(int32_t,1)
CF_CHECK_TYPE(u_int32_t,1)
CF_CHECK_TYPE(int64_t,1)
CF_CHECK_TYPE(u_int64_t,1)

AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_SIGNAL

AC_OUTPUT(
Makefile
conf/Makefile
docs/Makefile
scripts/Makefile
scripts/fo_userconf
scripts/fo_usermanagement
scripts/fo_xmlrpc
src/perl/Plugins/Makefile
src/perl/Plugins/Management/Makefile
src/Makefile
src/modules/Makefile
src/parsetpl/Makefile
src/templates/Makefile
src/templates/de/Makefile
src/templates/en/Makefile
src/templates/fr/Makefile
src/templates/de/tpl_html4/Makefile
src/templates/de/tpl_xhtml10/Makefile
src/templates/fr/tpl_html4/Makefile
src/templates/fr/tpl_xhtml10/Makefile
conf/fo_arcview.conf
conf/fo_default.conf
conf/fo_post.conf
conf/fo_userconf.conf
conf/fo_view.conf
conf/fo_server.conf
conf/fo_vote.conf
conf/fo_usermanagement.conf
conf/fo_xmlrpc.conf
Doxyfile
)

dnl eof


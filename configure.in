dnl
dnl \file configure.in
dnl \author Christian Kruse, <ckruse@wwwtech.de>
dnl
dnl configure input script for the classic forum project
dnl

dnl {{{ Initial comments
dnl $Header: /home/users/cvs/selfforum/configure.in,v 1.10 2003/11/24 12:53:27 ckruse Exp $
dnl $Log: configure.in,v $
dnl Revision 1.10  2003/11/24 12:53:27  ckruse
dnl bugfix with charset handling
dnl
dnl Revision 1.9  2003/10/20 08:57:39  ckruse
dnl Heavy developement
dnl
dnl
dnl }}}

sinclude(m4/ax_compare_version.m4)
sinclude(m4/ax_path_bdb.m4)
sinclude(m4/cf_check_type.m4)

AC_INIT

AM_CONFIG_HEADER(src/config.h)
AM_INIT_AUTOMAKE(cforum,2.0)

AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

dnl {{{ check for programs
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_PATH_PROG(RM,rm)
AC_PATH_PROG(LN,ln)
AC_PATH_PROG(LS,ls)
AC_PATH_PROG(GREP,grep)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(INSTALL,install)
AC_PATH_PROG(FLEX,flex)
AC_PATH_PROG(SED,sed)
AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(MAKEPGM,make)
dnl }}}

ALL_MODULES="flt_basic.la flt_category.la flt_deleted.la flt_frameset.la flt_http.la flt_link.la flt_listhighlight.la flt_livefilter.la flt_openclose.la flt_include.la flt_registerednames.la flt_tplmode.la flt_urlrewrite.la flt_visited.la flt_extern.la flt_threadreturnanchor.la flt_tipoftheday.la flt_postingassistant.la flt_remotesignature.la flt_preview.la flt_spellcheck.la flt_handle404.la flt_voting.la flt_scoring.la flt_motd.la flt_votingvariables.la flt_sorting.la flt_latex.la flt_syntax.la flt_checkregisteredname.la flt_mailonpost.la flt_cgiconfig.la flt_rot13.la flt_gummizelle.la flt_noanonymous.la flt_noanswer.la flt_chooser.la flt_moderated.la flt_cookieauth.la flt_noarchive.la flt_noarchive_server.la flt_stdreplacements.la flt_interesting.la flt_admin_mysql.la flt_shortcuts.la flt_phpsessauth.la flt_bbcodes.la flt_importexport.la flt_jsvalidation.la"

CFLAGS="$CFLAGS -D_REENTRANT -Wall -W -pipe -D_GNU_SOURCE"
LDFLAGS="$LDFLAGS -L/usr/local/lib"

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h pthread.h sys/ipc.h sys/shm.h sys/sem.h)

AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday mkdir mktime socket strerror strstr strtol strtoul strtoll strtoull snprintf sleep getenv)

AC_CHECK_FUNC(strdup,[],[AC_DEFINE(NOSTRDUP,1,[Defined if no strdup() is available])])
AC_CHECK_FUNC(strndup,[],[AC_DEFINE(NOSTRNDUP,1,[Defined if no strndup() is available])])
AC_CHECK_FUNC(getline,[],[AC_DEFINE(HAS_NO_GETLINE,1,[Defined if not getline() is available])])
AC_CHECK_FUNC(getdelim,[],[AC_DEFINE(HAS_NO_GETDELIM,1,[Defined if no getdelim is available])])

dnl
dnl check for libs and compiler specifics...
dnl

dnl check for iconv funktion
AC_CHECK_FUNC(iconv,ICONV="",[
  AC_CHECK_LIB(iconv,iconv,ICONV="-liconv",[AC_MSG_ERROR([iconv library cannot be found])])
])

dnl check for dlopen funktion
AC_CHECK_FUNC(dlopen,DL="",[
  AC_CHECK_LIB(dl,dlopen,DL="-ldl",[AC_MSG_ERROR([dl library cannot be found])])
])

dnl lets check for gdome-config and the right version of gdome
AC_PATH_PROG(GDOMECFG, gdome-config)
if test ! -n "$GDOMECFG" ; then
  AC_MSG_ERROR([cannot find gdome-config. Be sure that GDOME2 is installed and gdome-config is in your PATH])
fi

AC_PATH_PROG(PCRECFG, pcre-config)
if test ! -n "$PCRECFG" ; then
  AC_MSG_ERROR([cannot find pcre-config. Be sure that libpcre ist installed and pcre-config is in your PATH])
fi

AC_PATH_PROG(SWIG, swig)
if test ! -n "$SWIG" ; then
  AC_MSG_ERROR([cannot find swig. Be sure that SWIG is installed and it is in your PATH])
fi

AC_PATH_PROG(LIBWWWCFG, libwww-config)

if test ! -n "$LIBWWWCFG" ; then
  AC_MSG_ERROR([cannot find libwww-config. Be sure that libwww is installed and libwww-config is in your PATH])
fi

LIBWWW_CFLAGS=`$LIBWWWCFG --cflags`
LIBWWW_LIBS=`$LIBWWWCFG --libs`

GDOME_CFLAGS=`$GDOMECFG --cflags`
GDOME_LIBS=`$GDOMECFG --libs`

PCRE_CFLAGS=`$PCRECFG --cflags`
PCRE_LIBS=`$PCRECFG --libs`

CFLAGS="$GDOME_CFLAGS $CFLAGS $PCRE_CFLAGS $LIBWWW_CFLAGS"

AC_SUBST(GDOME_LIBS)
AC_SUBST(PCRE_LIBS)
AC_SUBST(LIBWWW_LIBS)
AC_SUBST(SWIG)
AC_SUBST(ICONV)
AC_SUBST(DL)


dnl lets check for -pthread
AC_MSG_CHECKING(for -pthread arg to compiler)
CFLAGS_SAVE="$CFLAGS"
CFLAGS="$CFLAGS -pthread"
AC_TRY_LINK([ #include <pthread.h> ],
  [ pthread_create(0,0,0,0); ],
  [
    AC_MSG_RESULT(yes)
    PTHREAD_LIB="-pthread"
  ],
  [
    AC_MSG_RESULT(no)
    CFLAGS="$CFLAGS_SAVE"
    AC_CHECK_LIB(pthread, pthread_create,[ PTHREAD_LIB="-lpthread" ],AC_MSG_ERROR([pthread library cannot be found]))
  ]
)

CFLAGS="$CFLAGS_SAVE"
AC_SUBST(PTHREAD_LIB)

AC_TRY_LINK([ #include <pthread.h> ],
  [ pthread_getprio(NULL); ],
  [ AC_DEFINE(_PTHREAD_GET_SET_PRIO,1,[Defined if pthread_get_prio() und pthread_set_prio() is available]) ],
  [ ]
)

AC_ARG_ENABLE(debug,
  [AC_HELP_STRING(
    [--enable-debug],
    [enable debugging mode])],
  [ AC_DEFINE(DEBUG,1,[Defined in debugging modus]) ],
  [ fake=1 ]
)

AC_ARG_ENABLE(sorting,
  [AC_HELP_STRING(
    [--enable-sorting],
    [enable sorting APIs (beware of the performance consequences!)])],
  [ fake=1 ],
  [ AC_DEFINE(CF_NO_SORTING,1,[Defined if sorting is not enabled]) ]
)

AX_PATH_BDB([3],[
    BDB_LIB="$BDB_LIBS";
    CFLAGS="$CFLAGS $BDB_CPPFLAGS"
  ],
  [AC_MSG_ERROR([no BerkeleyDB could be found])]
)

AC_CHECK_LIB(
  idn,
  idna_to_ascii_8z,
  IDN="-lidn",
  AC_MSG_ERROR([idn library cannot be found])
)

dnl {{{ check for modules to activate
AC_ARG_WITH(modules,
  [AC_HELP_STRING(
     [--with-modules=LIST],
     [Space seperated list of modules to enable])],
  [ OPTIONAL_MODULES="${with_modules}" ],
  [ OPTIONAL_MODULES="all" ]
)

AC_ARG_WITH(disable-modules,
  [AC_HELP_STRING(
    [--with-disable-modules=LIST],
    [space seperated list of modules to disable])],
  [ DISABLE_MODULES="${with_disable_modules}" ],
  [ fake=1 ]
)

if test "${OPTIONAL_MODULES}" = "all"; then
  OPTIONAL_MODULES="${ALL_MODULES}"
fi

for mod in ${DISABLE_MODULES}; do
  OPTIONAL_MODULES="`echo -n $OPTIONAL_MODULES | $SED \"s~\\s*$mod\\s*~~\"`";
done
AC_MSG_NOTICE([Activating modules: ${OPTIONAL_MODULES}])

for mod in ${OPTIONAL_MODULES}; do
  modname="`echo -n $mod | $SED 's!flt_!!' | $SED 's!\.la!!'`"
  eval "enable_${modname}=yes"
done
dnl }}}

dnl {{{ optional required libraries
dnl libesmtp is only necessary if flt_mailonpost is activated
if test "$enable_mailonpost" = "yes"; then
  AC_PATH_PROG(ESMTPCFG,libesmtp-config)

  if test ! -n "$ESMTPCFG" ; then
    AC_MSG_ERROR([cannot find libesmtp-config. Be sure that libesmtp is installed and libesmtp-config is in your PATH])
  fi

  ESMTP_CFLAGS=`$ESMTPCFG --cflags`
  ESMTP_LIBS=`$ESMTPCFG --libs`
fi

if test "$enable_admin_mysql" = "yes"; then
  AC_PATH_PROG(MYSQLCFG,mysql_config)

  if test ! -n "$MYSQLCFG"; then
    AC_MSG_ERROR([cannot find mysql_config. Be sure that mysql client libraries are installed and mysql_config is in your PATH])
  fi

  MYSQL_CFLAGS="`$MYSQLCFG --cflags`"
  MYSQL_LIBS="`$MYSQLCFG --libs`"
  MYSQL_LIBS_R="`$MYSQLCFG --libs_r`"
fi

AC_SUBST(ESMTP_LIBS)
AC_SUBST(MYSQL_LIBS)
AC_SUBST(MYSQL_LIBS_R)

CFLAGS="$CFLAGS $ESMTP_CFLAGS $MYSQL_CFLAGS"
dnl }}}


dnl
dnl shared memory is something like an after burner :)
dnl
AC_ARG_ENABLE(shared-mem,
  [AC_HELP_STRING(
    [--enable-shared-mem],
    [enable shared memory (*much* more performance)])],
  [ AC_DEFINE(CF_SHARED_MEM,1,[Defined if shared memory should be used]) ],
  [ fake=1 ]
)

AC_ARG_WITH(socket-path,
  [AC_HELP_STRING(
     [--with-socket-path=DIR],
     [the path where the socket should be located])],
  [fake=1],
  [ with_socket_path="/var/run/selfforum_socket" ]
)

AC_ARG_WITH(proto-file,
  [AC_HELP_STRING(
    [--with-proto-file=PATH],
    [the path where the protocol file for bad signals is located (used for debugging)])],
  [fake=1],
  [ with_proto_file="${localstatedir}/cforum/sigprotocol.txt" ]
)
AC_DEFINE_UNQUOTED(PROTOCOL_FILE,"${with_proto_file}",[needed to collect debugging infos])

AC_SUBST(DOCROOT)
AC_SUBST(DATADIR)
AC_SUBST(PERLMODULES)
AC_SUBST(CONFIGDIR)
AC_SUBST(IDN)
AC_SUBST(BDB_LIB)
AC_SUBST(with_socket_path)
AC_SUBST(OPTIONAL_MODULES)

dnl
dnl check for compiler characteristics
dnl

AC_CHECK_SIZEOF(short,"")
AC_CHECK_SIZEOF(int,"")
AC_CHECK_SIZEOF(long,"")
AC_CHECK_SIZEOF(long long,"")

CF_CHECK_TYPE(u_char,1)
CF_CHECK_TYPE(int16_t,1)
CF_CHECK_TYPE(u_int16_t,1)
CF_CHECK_TYPE(int32_t,1)
CF_CHECK_TYPE(u_int32_t,1)
CF_CHECK_TYPE(int64_t,1)
CF_CHECK_TYPE(u_int64_t,1)

AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_SIGNAL

AC_OUTPUT(
Makefile
repair.pl
conf/Makefile
conf/patterns/Makefile
docs/Makefile
scripts/Makefile
scripts/fo_admin
scripts/fo_userconf
scripts/fo_usermanagement
scripts/fo_xmlrpc
scripts/cf-cleanup.pl
scripts/cforum.sh
src/perl/Makefile
src/perl/Plugins/Makefile
src/perl/Plugins/Management/Makefile
src/swig/Makefile
src/swig/CForum-Configparser/Makefile.PL
src/swig/CForum-Validator/Makefile.PL
src/swig/CForum-Template/Makefile.PL
src/swig/CForum-Clientlib/Makefile.PL
src/Makefile
src/modules/Makefile
src/parsetpl/Makefile
conf/fo_admin.conf
conf/fo_arcview.conf
conf/fo_default.conf
conf/fo_post.conf
conf/fo_userconf.conf
conf/fo_view.conf
conf/fo_server.conf
conf/fo_vote.conf
conf/fo_usermanagement.conf
conf/fo_xmlrpc.conf
conf/fo_feeds.conf
conf/fo_cleanup.conf
Doxyfile
)

export prefix
export exec_prefix

$PERL repair.pl

dnl eof


dnl
dnl \file configure.in
dnl \author Christian Kruse, <ckruse@wwwtech.de>
dnl
dnl configure input script for the classic forum project
dnl

dnl {{{ Initial comments
dnl $Header: /home/users/cvs/selfforum/configure.in,v 1.10 2003/11/24 12:53:27 ckruse Exp $
dnl $Log: configure.in,v $
dnl Revision 1.10  2003/11/24 12:53:27  ckruse
dnl bugfix with charset handling
dnl
dnl Revision 1.9  2003/10/20 08:57:39  ckruse
dnl Heavy developement
dnl
dnl
dnl }}}

AC_INIT

AM_CONFIG_HEADER(src/config.h)
AM_INIT_AUTOMAKE(cforum,2.0)

AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL

CFLAGS="$CFLAGS -D_REENTRANT -Wall -W -pipe -D_GNU_SOURCE"
LDFLAGS="$LDFLAGS -L/usr/local/lib"

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h pthread.h sys/ipc.h sys/shm.h sys/sem.h)

AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday mkdir mktime socket strerror strstr strtol strtoul strtoll strtoull snprintf sleep getenv)

AC_CHECK_FUNC(strdup,[],[AC_DEFINE(NOSTRDUP,1,[Defined if no strdup() is available])])
AC_CHECK_FUNC(strndup,[],[AC_DEFINE(NOSTRNDUP,1,[Defined if no strndup() is available])])
AC_CHECK_FUNC(getline,[],[AC_DEFINE(HAS_NO_GETLINE,1,[Defined if not getline() is available])])
AC_CHECK_FUNC(getdelim,[],[AC_DEFINE(HAS_NO_GETDELIM,1,[Defined if no getdelim is available])])

dnl
dnl check for libs and compiler specifics...
dnl

dnl check for iconv funktion
AC_CHECK_FUNC(iconv,ICONV="",[
  AC_CHECK_LIB(iconv,iconv,ICONV="-liconv",AC_MSG_ERROR(iconv library cannot be found))
])

dnl check for dlopen funktion
AC_CHECK_FUNC(dlopen,DL="",[
  AC_CHECK_LIB(dl,dlopen,DL="-ldl",AC_MSG_ERROR(dl library cannot be found))
])

dnl search for bash...
AC_PATH_PROG(BASH,bash)
if test ! -n "$BASH" ; then
  AC_MSG_ERROR(cannot find bash. Be sure that bash2 is installed and is in your PATH)
fi

dnl lets check for gdome-config and the right version of gdome
AC_PATH_PROG(GDOMECFG, gdome-config)
if test ! -n "$GDOMECFG" ; then
  AC_MSG_ERROR(cannot find gdome-config. Be sure that GDOME2 is installed and gdome-config is in your PATH)
fi

AC_PATH_PROG(LIBWWWCFG, libwww-config)
if test ! -n "$LIBWWWCFG" ; then
  AC_MSG_ERROR(cannot find libwww-config. Be sure that libwww is installed and libwww-config is in your PATH)
fi

GDOME_CFLAGS=`$GDOMECFG --cflags`
GDOME_LIBS=`$GDOMECFG --libs`

LIBWWW_CFLAGS=`$LIBWWWCFG --cflags`
LIBWWW_LIBS=`$LIBWWWCFG --libs`

CFLAGS="$LIBWWW_CFLAGS $GDOME_CFLAGS $CFLAGS"

AC_SUBST(LIBWWW_LIBS)
AC_SUBST(GDOME_LIBS)
AC_SUBST(ICONV)
AC_SUBST(DL)


dnl lets check for -pthread
AC_MSG_CHECKING(for -pthread arg to compiler)
CFLAGS_SAVE="$CFLAGS"
CFLAGS="$CFLAGS -pthread"
AC_TRY_LINK([ #include <pthread.h> ],
  [ pthread_create(0,0,0,0); ],
  [
    AC_MSG_RESULT(yes)
    PTHREAD_LIB="-pthread"
  ],
  [
    AC_MSG_RESULT(no)
    CFLAGS="$CFLAGS_SAVE"
    AC_CHECK_LIB(pthread, pthread_create,[ PTHREAD_LIB="-lpthread" ],AC_MSG_ERROR(pthread library cannot be found))
  ]
)

CFLAGS="$CFLAGS_SAVE"
AC_SUBST(PTHREAD_LIB)

AC_TRY_LINK([ #include <pthread.h> ],
  [ pthread_getprio(NULL); ],
  [ AC_DEFINE(_PTHREAD_GET_SET_PRIO,1,[Defined if pthread_get_prio() und pthread_set_prio() is available]) ],
  [ ]
)

AC_ARG_ENABLE(debug,
  [  --enable-debug          enable debugging mode],
  [
    AC_DEFINE(DEBUG,1,[Defined in debugging modus])
#    CFLAGS="-g -O0 $CFLAGS"
  ],
#  [ CFLAGS="$CFLAGS -O3 -fomit-frame-pointer" ]
  [ fake=1 ]
)

dnl
dnl shared memory is something like an after burner :)
dnl
AC_ARG_ENABLE(shared-mem,
  [  --enable-shared-mem     enable shared memory (*much* more performance)],
  [ AC_DEFINE(CF_SHARED_MEM,1,[Defined if shared memory should be used]) ],
  [ fake=1 ]
)

AC_ARG_WITH(template-dir,
  [  --with-template-dir=DIR the directory where the templates are located],[fake=1],
  [ with_template_dir="`pwd`/cgi-shared/config/templates" ]
)
AC_ARG_WITH(config-dir,
  [  --with-config-dir       the directory where the config files should be copied to],[fake=1],
  [ with_config_dir="`pwd`/cgi-shared/config" ]
)
AC_ARG_WITH(compiled-template-dir,
  [  --with-compiled-templatedir=DIR          the directory where the compiled templates are located],[fake=1],
  [ with_compiled_template_dir="`pwd`/cgi-shared/config/ctemplates" ]
)
AC_ARG_WITH(modules-dir,
  [  --with-modules-dir=DIR  the directory where the plugins are located],[fake=1],
  [ with_modules_dir="`pwd`/cgi-shared/modules" ]
)
AC_ARG_WITH(message-dir,
  [  --with-message-dir=DIR  the directory where the message files are located],[fake=1],
  [ with_message_dir="`pwd`/cgi-shared/data/messages" ]
)
AC_ARG_WITH(archive-dir,
  [  --with-archive-dir=DIR  the directory where the archived files are located],[fake=1],
  [ with_archive_dir="`pwd`/cgi-shared/data/archive" ]
)
AC_ARG_WITH(socket-path,
  [  --with-socket-path=DIR  the path where the socket should be located],[fake=1],
  [ with_socket_path="/tmp/selfforum_socket" ]
)
AC_ARG_WITH(log-dir,
  [  --with-log-dir=DIR      the path where the forum log files should be located],[fake=1],
  [ with_log_dir="`pwd`/logs" ]
)
AC_ARG_WITH(userconfig-dir,
  [  --with-userconfig-dir=DIR the path where the user configuration files are located],[fake=1],
  [ with_userconfig_dir="${with_config_dir}/users/" ]
)
AC_ARG_WITH(owner,
  [  --with-owner=OWNER      the owner of the forum files],[fake=1],
  [ with_owner=www ]
)
AC_ARG_WITH(group,
  [  --with-group=GROUP      the group of the forum files],[fake=1],
  [ with_group=www ]
)
AC_ARG_WITH(cgi-bin,
  [  --with-cgi-bin=DIR      the cgi-bin directory],[fake=1],
  [ with_cgi_bin="`pwd`/cgi-bin" ]
)
AC_ARG_WITH(cgi-shared,
  [  --with-cgi-shared=DIR   the cgi-bin directory],[fake=1],
  [ with_cgi_shared="`pwd`/cgi-shared" ]
)
AC_ARG_WITH(src-dir,
  [  --with-src-dir=DIR      the cgi-bin directory],[fake=1],
  [ with_src_dir="`pwd`/src" ]
)
AC_ARG_WITH(lib-dir,
  [  --with-lib-dir=DIR      the forum libraries directory],[fake=1],
  [ with_lib_dir="`pwd`/cgi-shared/lib/" ]
)

AC_ARG_WITH(bdb-include,
  [  --with-bdb-include=DIR the bdb directory including the header files],
  [ CFLAGS="$CFLAGS -I${withval}" ],
  [ fake=1 ]
)
AC_ARG_WITH(bdb-lib,
  [  --with-bdb-lib=NAME    the bdb library name (e.g. libdb4.so)],
  [ BDB_LIB="${withval}" ],
  [ fake=1 ]
)


AC_CHECK_LIB(
  idn,
  idna_to_ascii_8z,
  IDN="-lidn",
  AC_MSG_ERROR(idn library cannot be found)
)

AC_SUBST(IDN)
AC_SUBST(BDB_LIB)
AC_SUBST(with_lib_dir)
AC_SUBST(with_src_dir)
AC_SUBST(with_template_dir)
AC_SUBST(with_config_dir)
AC_SUBST(with_compiled_template_dir)
AC_SUBST(with_modules_dir)
AC_SUBST(with_message_dir)
AC_SUBST(with_archive_dir)
AC_SUBST(with_socket_path)
AC_SUBST(with_log_dir)
AC_SUBST(with_userconfig_dir)
AC_SUBST(with_owner)
AC_SUBST(with_group)
AC_SUBST(with_cgi_bin)
AC_SUBST(with_cgi_shared)

dnl
dnl check for programs
dnl
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(LN,ln)
AC_PATH_PROG(LS,ls)
AC_PATH_PROG(GREP,grep)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(INSTALL,install)
AC_PATH_PROG(FLEX,flex)
AC_PATH_PROG(SED,sed)
AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(MAKEPGM,make)

dnl
dnl check for compiler characteristics
dnl

AC_CHECK_SIZEOF(short,"")
AC_CHECK_SIZEOF(int,"")
AC_CHECK_SIZEOF(long,"")
AC_CHECK_SIZEOF(long long,"")

dnl
dnl AC_CHECK_TYPE sucks
dnl
AC_DEFUN(CF_CHECK_TYPE,
  [
    AC_MSG_CHECKING(for $1)
    AC_CACHE_VAL(ac_cv_cf_have_$1,
      AC_TRY_COMPILE(
        [
#include <sys/types.h>
#include <stdlib.h>
#include <stddef.h>
        ],
        [$1 vr],
        ac_cv_cf_have_$1=yes,
        ac_cv_cf_have_$1=no
      )
    )
    AC_MSG_RESULT($ac_cv_cf_have_$1)
    if test "$ac_cv_cf_have_$1" = "no"; then
      AC_DEFINE(DONT_HAS_$1,$2,[Defined if we haven't got $1])
    fi
  ]
)

CF_CHECK_TYPE(u_char,1)
CF_CHECK_TYPE(int16_t,1)
CF_CHECK_TYPE(u_int16_t,1)
CF_CHECK_TYPE(int32_t,1)
CF_CHECK_TYPE(u_int32_t,1)
CF_CHECK_TYPE(int64_t,1)
CF_CHECK_TYPE(u_int64_t,1)

AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_SIGNAL

AC_OUTPUT(Makefile src/Makefile src/modules/Makefile cgi-shared/config/templates/tpl_html4/Makefile cgi-shared/config/templates/tpl_xhtml10/Makefile cgi-shared/config/fo_arcview.conf cgi-shared/config/fo_default.conf cgi-shared/config/fo_post.conf cgi-shared/config/fo_userconf.conf cgi-shared/config/fo_view.conf cgi-shared/config/fo_server.conf Doxyfile)

dnl eof

